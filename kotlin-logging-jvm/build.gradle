apply plugin: 'kotlin-platform-jvm'

buildscript {

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}
sourceCompatibility = 1.6

repositories {
    mavenCentral()
}

sourceSets {
    main {
        kotlin {
            srcDirs = ['src', '../kotlin-logging-common/src']
        }
    }
}

dependencies {
    expectedBy project(':kotlin-logging-common')
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.slf4j:slf4j-api:$sl4j_version"
    testCompile "org.slf4j:slf4j-log4j12:$sl4j_version"
    testCompile "log4j:log4j:1.2.17"
    testCompile "org.mockito:mockito-all:1.10.19"
    testCompile 'junit:junit:4.12'
}

// kotlin compiler compatibility options
compileKotlin {
    kotlinOptions {
        apiVersion = "1.0"
        languageVersion = "1.0"
    }
}

// custom tasks for creating source/javadoc jars
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

// add javadoc/source jar tasks as artifacts
artifacts {
    archives sourcesJar, javadocJar
}

